
data "vsphere_datacenter" "dc" {
  name = var.vs_datacenter
}

data "vsphere_compute_cluster" "cluster" {
  name          = var.vs_cluster
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_datastore" "datastore" {
  name          = var.vs_datastore
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_virtual_machine" "template" {
  name = var.vm_template
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_network" "network1" {
  name          = var.vs_network_1
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_network" "network2" {
  name          = var.vs_network_2
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_network" "network3" {
  name          = var.vs_network_3
  datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_network" "network4" {
    name          = var.vs_network_4 != "" ? var.vs_network_4 : "2"
    datacenter_id = data.vsphere_datacenter.dc.id
}

data "vsphere_resource_pool" "rpool" {
  name              = "/${var.vs_datacenter}/host/${var.vs_cluster}/Resources/${var.vs_resourcepool}"
  datacenter_id = data.vsphere_datacenter.dc.id
}

resource "vsphere_virtual_machine" "vm" {
  name = "${var.vm_prefix}-${var.vm_suffix}-${var.vm_name}"
  datastore_id = data.vsphere_datastore.datastore.id
  resource_pool_id = data.vsphere_resource_pool.rpool.id
  #resource_pool_id = data.vsphere_compute_cluster.cluster.resource_pool_id
  folder = var.vm_folder
  firmware = "efi"
  num_cpus = var.vm_cpu_map["${var.vm_cpu}"]
  memory = var.vm_memory_map["${var.vm_memory}"]
  guest_id = data.vsphere_virtual_machine.template.guest_id
  wait_for_guest_ip_timeout = 300
  wait_for_guest_net_timeout = 300
  wait_for_guest_net_routable = false
  migrate_wait_timeout = 90
  cpu_hot_add_enabled    = true
  cpu_hot_remove_enabled = true
  memory_hot_add_enabled = true
  network_interface {
    network_id = data.vsphere_network.network1.id
    adapter_type = data.vsphere_virtual_machine.template.network_interface_types[0]
  }

  dynamic "network_interface" {
    for_each = var.vs_network_2 != "" ? [1] : []
    content {
       network_id = data.vsphere_network.network2.id
       adapter_type = data.vsphere_virtual_machine.template.network_interface_types[0]
    }
  }

  dynamic "network_interface" {
    for_each = var.vs_network_3 != "" ? [1] : []
    content {
       network_id = data.vsphere_network.network3.id
       adapter_type = data.vsphere_virtual_machine.template.network_interface_types[0]
    }
  }

  dynamic "network_interface" {
    for_each = var.vs_network_4 != "" ? [1] : []
    content {
       network_id = data.vsphere_network.network4.id
       adapter_type = data.vsphere_virtual_machine.template.network_interface_types[0]
    }
  }

  scsi_controller_count = 1
  scsi_type = data.vsphere_virtual_machine.template.scsi_type

  disk {
     label = "disk0"
     size = data.vsphere_virtual_machine.template.disks.0.size
     eagerly_scrub = data.vsphere_virtual_machine.template.disks.0.eagerly_scrub
     thin_provisioned = data.vsphere_virtual_machine.template.disks.0.thin_provisioned
     unit_number = 0
  }

  dynamic "disk" {
    for_each = var.vm_datadsk != "" ? [1] : []
    content {
       label = "disk1"
       size = var.vm_datadsk_map["${var.vm_datadsk}"]
       thin_provisioned = true
       eagerly_scrub = false
       unit_number = 14
    }
  }

  clone {
     template_uuid = data.vsphere_virtual_machine.template.id
     customize {
         timeout = 0

         dynamic "windows_options" {
            for_each = var.vm_is_windows == true ? [1] : []
            content {
                      computer_name = "${var.vm_prefix}-${var.vm_suffix}-${var.vm_name}"
            }
          }

         dynamic "linux_options" {
            for_each = var.vm_is_windows == false ? [1] : [] 
            content {
                       host_name = "${var.vm_prefix}-${var.vm_suffix}-${var.vm_name}"
                       domain = var.vm_dns_search
            }
          }

          network_interface {
               ipv4_address = var.vm_ipaddress_1 != "" ? var.vm_ipaddress_1 : null
               ipv4_netmask = var.vm_netmask_1 != "" ? var.vm_netmask_1 : null
               dns_domain   = var.vm_ipaddress_1 != "" ? var.vm_dns_search : null
               dns_server_list =  var.vm_ipaddress_1 != "" ? [var.vm_dns_list] : null
          }
        
          dynamic "network_interface" {
            for_each = var.vm_ipaddress_2 != "" ? [1] : []  
            content {
              ipv4_address = var.vm_ipaddress_2 != "" ? var.vm_ipaddress_2 : null
              ipv4_netmask = var.vm_netmask_2 != "" ? var.vm_netmask_2 : null
            }
          }

          dynamic "network_interface" {
            for_each = var.vm_ipaddress_3 != "" ? [1] : []  
            content {
              ipv4_address = var.vm_ipaddress_3 != "" ? var.vm_ipaddress_3 : null
              ipv4_netmask = var.vm_netmask_3 != "" ? var.vm_netmask_3 : null
            }
          }

          dynamic "network_interface" {
            for_each = var.vm_ipaddress_4 != "" ? [1] : []
            content {
              ipv4_address = var.vm_ipaddress_4 != "" ? var.vm_ipaddress_4 : null
              ipv4_netmask = var.vm_netmask_4 != "" ? var.vm_netmask_4 : null
            }
          }


      ipv4_gateway = var.vm_gateway != "" ? var.vm_gateway : null
     }
  }
}
